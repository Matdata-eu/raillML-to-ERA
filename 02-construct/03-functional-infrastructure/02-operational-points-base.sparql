PREFIX era: <http://data.europa.eu/949/>
PREFIX country: <http://publications.europa.eu/resource/authority/country/>
PREFIX gsp: <http://www.opengis.net/ont/geosparql#>
PREFIX railml: <https://www.railml.org/schemas/3.2#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Phase 3.2 — Operational Points (functional infrastructure)
# Maps railml:operationalPoint → era:OperationalPoint
#
# Source: 10 railML operational point instances
#
# Target properties (SHACL-compliant):
#   - era:opName — operational point name (optional)
#   - era:opType — REQUIRED (exactly 1) SKOS concept
#   - era:uopid — REQUIRED (exactly 1) unique operational point identifier
#   - era:infrastructureManager → era:OrganisationRole (hardcoded to 0076_IM for workshop)
##
# Geometry structure (Strategy 2 only, from spotElementProjection):
#   - gsp:asWKT → WKT POINT literal
#   - Coordinates transformed from railML visualization to WGS84
#
# URI Patterns:
#   OperationalPoint: http://data.europa.eu/949/functionalInfrastructure/operationalPoints/{id}
#   Geometry: http://data.europa.eu/949/geometry/{?coordLon}/{?coordLat}
#   OrganisationRole: http://data.europa.eu/949/organisations/0076_IM (hardcoded for workshop)

CONSTRUCT {
  # Operational Point
  ?eraOp a era:OperationalPoint ;
         era:opName ?opName, ?opNameEn ;
         rdfs:label ?opName, ?opNameEn ;
         era:opType ?eraOpType ;
         era:uopid ?uopid ;
         era:inCountry country:NOR ;
         era:infrastructureManager <http://data.europa.eu/949/organisations/0076_IM>;
         era:hasSchematicOverviewOPDigitalForm "true"^^xsd:boolean ; # This is the schematic topology we are using from the screencoordinates
         era:referenceBorderPoint ?refBorderPoint ;
         . 
         
 ?refBorderPoint a era:ReferenceBorderPoint ;
    era:borderPointId ?uopid ;
}
WHERE {
  # Get all operational points
  ?op a railml:operationalPoint ;
      xyz:id ?oppId .

  # Mint ERA OperationalPoint URI
  BIND (IRI(CONCAT("http://data.europa.eu/949/functionalInfrastructure/operationalPoints/", ?oppId)) AS ?eraOp)

  # Get operational point name
  OPTIONAL {
    ?op fx:name ?nameNode .
    ?nameNode xyz:name ?nameValue .
    OPTIONAL {
      ?nameNode xyz:language ?lang
    }
    BIND (IF(BOUND(?lang),STRLANG(?nameValue,?lang),?nameValue) AS ?opName)
    BIND (IF(BOUND(?lang),STRLANG(?nameValue,"en"),?nameValue) AS ?opNameEn) # need an English name
  }

  # Get operational type and map to ERA concept
  ?op fx:opOperations ?opOperations .
  ?opOperations fx:opOperation ?opOperation .
  ?opOperation xyz:operationalType ?opTypeRailML .
  
  # Map railML operational types to ERA concepts
  # Common types: station, passengerStop, borderPoint, junction, etc.
  BIND (
    IF(?opTypeRailML = "station",        IRI("http://data.europa.eu/949/concepts/op-types/10"),
    IF(?opTypeRailML = "stoppingPoint",  IRI("http://data.europa.eu/949/concepts/op-types/70"),
    IF(?opTypeRailML = "borderPoint",    IRI("http://data.europa.eu/949/concepts/op-types/90"),
    IF(?opTypeRailML = "siding",         IRI("http://data.europa.eu/949/concepts/op-types/80"),
    IF(?opTypeRailML = "junction",       IRI("http://data.europa.eu/949/concepts/op-types/80"),
    IF(?opTypeRailML = "passengerStop",  IRI("http://data.europa.eu/949/concepts/op-types/70"),
    IRI("http://data.europa.eu/949/concepts/op-types/10")  # Default to station
    ))))))
    AS ?eraOpType
  )

  # Get designator for UOPID
  ?op fx:designator ?designatorNode .
  ?designatorNode xyz:register '_railML' ;
                    xyz:entry ?uopid_suffix .
                    
  BIND(IF(BOUND(?eraOpType) &&?eraOpType=IRI("http://data.europa.eu/949/concepts/op-types/90"),"EU","NO") as ?uopid_prefix)
  BIND(IF(BOUND(?eraOpType) &&?eraOpType=IRI("http://data.europa.eu/949/concepts/op-types/90"),IRI(CONCAT("http://data.europa.eu/949/functionalInfrastructure/borderpoint/", ?oppId)),?UNDEF) as ?refBorderPoint)

  BIND(CONCAT(?uopid_prefix, ?uopid_suffix) as ?uopid )

  # Geometry from spotElementProjection
  OPTIONAL {
    ?infVis a railml:infrastructureVisualization .
    ?infVis fx:spotElementProjection ?spotElemProj .
    ?spotElemProj xyz:refersToElement ?oppId ;
                  fx:coordinate ?coordNode .
    ?coordNode xyz:x ?xStr ;
                xyz:y ?yStr .
    BIND (3.0 + xsd:decimal(?xStr) / 10000.0 AS ?coordLon)
    BIND (53.0 + xsd:decimal(?yStr) / 10000.0 AS ?coordLat)
    BIND (IRI(CONCAT("http://data.europa.eu/949/geometry/", STR(?coordLon), "/", STR(?coordLat))) AS ?geometry)
    BIND (STRDT(CONCAT("POINT(", STR(?coordLon), " ", STR(?coordLat), ")"), gsp:wktLiteral) AS ?wkt)
  }
}
ORDER BY ?oppId
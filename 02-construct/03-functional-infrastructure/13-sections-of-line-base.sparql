PREFIX era: <http://data.europa.eu/949/>
PREFIX gsp: <http://www.opengis.net/ont/geosparql#>
PREFIX country: <http://publications.europa.eu/resource/authority/country/>
PREFIX railml: <https://www.railml.org/schemas/3.2#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Phase 3.9 — Sections of Line (Base Properties)
# Maps railml:line → era:SectionOfLine
#
# Mapping logic:
#   - Lines WITH belongsToParent → era:SectionOfLine (actual sections)
#   - Lines WITHOUT belongsToParent → era:nationalLine property value (parent line identifier)
#
# For sections (lines with belongsToParent):
#   - opStart/opEnd extracted from line name (format: "[opStart]-[opEnd]")
#   - Operational points are matched by name
#
# Target properties:
#   era:infrastructureManager → era:Body
#   era:inCountry → xsd:string (country code)
#   era:solNature → skos:Concept
#   era:nationalLine → xsd:string (from parent line name)
#   era:opStart → era:OperationalPoint
#   era:opEnd → era:OperationalPoint
#
# URI Pattern: https://data.matdata.eu/_sectionsOfLine_{id}

CONSTRUCT {
  ?eraSol a era:SectionOfLine ;
          rdfs:label ?lineName;
          era:infrastructureManager <http://data.europa.eu/949/organisations/0076_IM> ;
          era:inCountry country:NOR ;
          era:solNature ?eraSolNature ;
          era:nationalLine ?thisDoesntMakeSense ; ;
          era:opStart ?eraOpStart ;
          era:opEnd ?eraOpEnd .

  ?thisDoesntMakeSense a era:LinearPositioningSystem ;
          rdfs:label "This doesn't make sense"@en ;
          era:lineId ?nationalLineNameStr .
}
WHERE {
  # Get lines WITH belongsToParent (child lines = actual sections of line)
  ?line a railml:line ;
        xyz:id ?lineId ;
        xyz:belongsToParent ?parentLineId .

  # Get line name (format: "[opStartName]-[opEndName]")
  OPTIONAL {
    ?line fx:name ?nameNode .
    ?nameNode xyz:name ?nameValue .
    OPTIONAL {
      ?nameNode xyz:language ?lang
    }
    BIND (IF(BOUND(?lang),STRLANG(?nameValue,?lang),?nameValue) AS ?lineName)
  }

  # Mint ERA SectionOfLine URI
  BIND (IRI(CONCAT("https://data.matdata.eu/_sectionsOfLine_", ?lineId)) AS ?eraSol)

  # Get parent line to extract nationalLine name
  ?parentLine a railml:line ;
              xyz:id ?parentLineId ;
              fx:name ?parentNameNode .
  ?parentNameNode xyz:name ?nationalLineNameStr ;
                  xyz:language ?nationalLineLang .
  
  # Create language-tagged string for nationalLine
  BIND (IRI(CONCAT("https://data.matdata.eu/_lps_", ?nationalLineNameStr)) AS ?thisDoesntMakeSense)

  # Extract operational point names from line name
  # Line name format: "[opStart]-[opEnd]"
  BIND (STR(?nameValue) AS ?lineNameStr)
  BIND (STRBEFORE(?lineNameStr, "-") AS ?opStartName)
  BIND (STRAFTER(?lineNameStr, "-") AS ?opEndName)

  # Find operational points by matching their names
  # Find opStart
  ?opStart a railml:operationalPoint ;
            xyz:id ?opStartId ;
            fx:designator/xyz:entry ?opStartName .
  BIND (IRI(CONCAT("https://data.matdata.eu/_operationalPoints_", ?opStartId)) AS ?eraOpStart)

  # Find opEnd
  ?opEnd a railml:operationalPoint ;
          xyz:id ?opEndId ;
          fx:designator/xyz:entry ?opEndName .
  BIND (IRI(CONCAT("https://data.matdata.eu/_operationalPoints_", ?opEndId)) AS ?eraOpEnd)


  # Default: solNature = "Line"
  BIND (IRI("http://data.europa.eu/949/concepts/sol-natures/10") AS ?eraSolNature)
}
